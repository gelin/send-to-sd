// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
    }
}

repositories {
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 45
        versionName '0.3.11'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }

//    testOptions {
//        execution 'ANDROID_TEST_ORCHESTRATOR'
//    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
}

if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
} else {
    android.signingConfigs.release.storePassword = System.getenv('STORE_PASSWORD')
}
if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.signingConfigs.release.keyPassword = System.getenv('KEY_PASSWORD')
}

dependencies {
    implementation 'com.android.support:support-compat:25.4.0'
    implementation 'com.android.billingclient:billing:dp-1'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support:support-annotations:25.4.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.0'
}
